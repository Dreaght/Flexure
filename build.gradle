plugins {
  id 'java'
  id 'application'
  id 'org.javamodularity.moduleplugin' version '1.8.12'
  id 'org.openjfx.javafxplugin' version '0.0.13'
  id 'org.beryx.jlink' version '2.25.0'
}

group 'org.dreaght'
version '2.0'

repositories {
  mavenCentral()
  maven {
    url = "https://repository.aspose.com/repo/"
  }
}

ext {
  junitVersion = '5.10.0'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

application {
  mainModule = 'org.dreaght.flexure'
  mainClass = 'org.dreaght.flexure.FlexureApplication'
}

javafx {
  version = '17.0.6'
  modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
  compileOnly 'org.projectlombok:lombok:1.18.32'
  annotationProcessor 'org.projectlombok:lombok:1.18.32'

  testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

  implementation("com.aspose:aspose-cad:24.3")
}

test {
  useJUnitPlatform()
}

jlink {
  imageZip = project.file("${buildDir}/distributions/Flexure-${version}.zip")
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
  addOptions '--add-modules', 'aspose.cad,javafx.controls,javafx.fxml'
  launcher {
    name = 'app'
  }
  targetPlatform("win") {
    jdkHome = jdkDownload("https://github.com/AdoptOpenJDK/semeru17-binaries/releases/download/jdk-17.0.11%2B9_openj9-0.44.0/ibm-semeru-open-jdk_x64_windows_17.0.11_9_openj9-0.44.0.zip")
  }
//  targetPlatform("mac") {
//    jdkHome = jdkDownload("https://github.com/AdoptOpenJDK/semeru17-binaries/releases/download/jdk-17.0.11%2B9_openj9-0.44.0/ibm-semeru-open-jdk_aarch64_mac_17.0.11_9_openj9-0.44.0.tar.gz")
//  }
}

jlinkZip {
  group = 'distribution'
}

def jpackageOutputDir = "$buildDir/jpackage"

task jpackageWindows(type: Exec) {
  dependsOn tasks.jlink
  onlyIf { org.gradle.internal.os.OperatingSystem.current().isWindows() }
  doFirst {
    mkdir(jpackageOutputDir)
  }
  commandLine 'jpackage', '--type', 'exe',
          '--dest', jpackageOutputDir,
          '--name', 'Flexure',
          '--module', 'org.dreaght.flexure/org.dreaght.flexure.FlexureApplication',
          '--runtime-image', "${buildDir}/image",
          '--main-class', 'org.dreaght.flexure.FlexureApplication'
  //    '--icon', 'path/to/icon.ico' // Optional: path to your app icon
}

task jpackageLinux(type: Exec) {
  dependsOn tasks.jlink
  onlyIf { org.gradle.internal.os.OperatingSystem.current().isLinux() }
  doFirst {
    mkdir(jpackageOutputDir)
  }
  commandLine 'jpackage', '--type', 'rpm',
          '--dest', jpackageOutputDir,
          '--name', 'Flexure',
          '--module', 'org.dreaght.flexure/org.dreaght.flexure.FlexureApplication',
          '--runtime-image', "${buildDir}/image",
          '--main-class', 'org.dreaght.flexure.FlexureApplication'
  //    '--icon', 'path/to/icon.png' // Optional: path to your app icon
}

task jpackageMac(type: Exec) {
  dependsOn tasks.jlink
  onlyIf { org.gradle.internal.os.OperatingSystem.current().isMacOsX() }
  doFirst {
    mkdir(jpackageOutputDir)
  }
  commandLine 'jpackage', '--type', 'pkg',
          '--dest', jpackageOutputDir,
          '--name', 'Flexure',
          '--module', 'org.dreaght.flexure/org.dreaght.flexure.FlexureApplication',
          '--runtime-image', "${buildDir}/image",
          '--main-class', 'org.dreaght.flexure.FlexureApplication'
  //    '--icon', 'path/to/icon.icns' // Optional: path to your app icon
}

// Define a task to run all jpackage tasks
task jpackageAll {
  dependsOn jpackageWindows, jpackageLinux, jpackageMac
}
